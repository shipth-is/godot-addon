## High-level API methods for ShipThis backend

const ApiClient = preload("res://addons/shipthis/lib/api_client.gd")

var client: ApiClient = null


func _init() -> void:
	client = ApiClient.new()


func set_token(token: String) -> void:
	client.set_token(token)


## Low-level POST request passthrough for auth and other generic calls
func post(path: String, body: Dictionary = {}) -> Dictionary:
	return await client.post(path, body)


## Get an upload ticket for a project. Returns {is_success, data: {id, url}} on success.
func get_upload_ticket(project_id: String) -> Dictionary:
	return await client.post("/upload/%s/url" % project_id, {})


## Start jobs for an uploaded file. Returns {is_success, data: Job[]} on success.
func start_jobs(upload_ticket_id: String) -> Dictionary:
	return await client.post("/upload/start/%s" % upload_ticket_id, {})


## Get project by ID
func get_project(project_id: String) -> Dictionary:
	return await client.fetch("/projects/%s" % project_id)


## Get credentials for a project
func get_project_credentials(project_id: String) -> Dictionary:
	return await client.fetch("/projects/%s/credentials" % project_id)


## Get Google connection status
func get_google_status() -> Dictionary:
	return await client.fetch("/google/status")


## Query builds for a project
func query_builds(project_id: String, page: int = 0) -> Dictionary:
	return await client.fetch("/builds?projectId=%s&pageNumber=%d" % [project_id, page])


## Test service account key
func fetch_key_test_result(project_id: String) -> Dictionary:
	return await client.fetch("/projects/%s/key-test" % project_id)


## Create a new project
func create_project(name: String, details: Dictionary) -> Dictionary:
	return await client.post("/projects", {"name": name, "details": details})


## Update an existing project
func update_project(project_id: String, data: Dictionary) -> Dictionary:
	return await client.put("/projects/%s" % project_id, data)


## Create a new Android keystore (generated by backend)
func create_android_keystore(project_id: String) -> Dictionary:
	return await client.post("/projects/%s/credentials/android/certificate" % project_id, {})


## Get a presigned URL for credential import
## Returns {is_success, data: {url, uuid}}
func get_credential_import_ticket(project_id: String) -> Dictionary:
	return await client.post("/projects/%s/credentials/import/url" % project_id, {})


## Trigger credential import after uploading ZIP
func trigger_credential_import(project_id: String, platform: String, type: String, uuid: String) -> Dictionary:
	return await client.post("/projects/%s/credentials/import" % project_id, {
		"platform": platform,
		"type": type,
		"uuid": uuid
	})
