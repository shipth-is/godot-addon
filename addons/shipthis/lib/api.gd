## High-level API methods for ShipThis backend

const ApiClient = preload("res://addons/shipthis/lib/api_client.gd")

var client: ApiClient = null


func _init() -> void:
	client = ApiClient.new()


func set_token(token: String) -> void:
	client.set_token(token)


## Low-level POST request passthrough for auth and other generic calls
func post(path: String, body: Dictionary = {}) -> Dictionary:
	return await client.post(path, body)


## Get an upload ticket for a project. Returns {is_success, data: {id, url}} on success.
func get_upload_ticket(project_id: String) -> Dictionary:
	return await client.post("/upload/%s/url" % project_id, {})


## Start jobs for an uploaded file. Returns {is_success, data: Job[]} on success.
## Options can include: platform (e.g. "ANDROID"), skipPublish, etc.
func start_jobs(upload_ticket_id: String, options: Dictionary = {}) -> Dictionary:
	return await client.post("/upload/start/%s" % upload_ticket_id, options)


## Get project by ID
func get_project(project_id: String) -> Dictionary:
	return await client.fetch("/projects/%s" % project_id)


## Get credentials for a project
func get_project_credentials(project_id: String) -> Dictionary:
	return await client.fetch("/projects/%s/credentials" % project_id)


## Get platform progress (ANDROID or IOS). Returns {is_success, data: {hasBundleSet, hasCredentialsForPlatform, hasApiKeyForPlatform, hasSuccessfulJobForPlatform, platform}}
func get_project_platform_progress(project_id: String, platform: String) -> Dictionary:
	return await client.fetch("/projects/%s/%s/progress" % [project_id, platform])


## Get Google connection status
func get_google_status() -> Dictionary:
	return await client.fetch("/me/google/status")


## Query builds for a project
func query_builds(project_id: String, page: int = 0) -> Dictionary:
	return await client.fetch("/projects/%s/builds?pageNumber=%d" % [project_id, page])


## Query jobs for a project
func query_jobs(project_id: String, page: int = 0) -> Dictionary:
	return await client.fetch("/projects/%s/jobs?pageNumber=%d" % [project_id, page])


## Test service account key
func fetch_key_test_result(project_id: String) -> Dictionary:
	return await client.post("/projects/%s/credentials/android/key/test" % project_id, {})


## Create a new project
func create_project(name: String, details: Dictionary) -> Dictionary:
	return await client.post("/projects", {"name": name, "details": details})


## Update an existing project
func update_project(project_id: String, data: Dictionary) -> Dictionary:
	return await client.put("/projects/%s" % project_id, data)


## Create a new Android keystore (generated by backend)
func create_android_keystore(project_id: String) -> Dictionary:
	return await client.post("/projects/%s/credentials/android/certificate" % project_id, {})


## Get a presigned URL for credential import
## Returns {is_success, data: {url, uuid}}
func get_credential_import_ticket(project_id: String) -> Dictionary:
	return await client.post("/projects/%s/credentials/import/url" % project_id, {})


## Trigger credential import after uploading ZIP
func trigger_credential_import(project_id: String, platform: String, type: String, uuid: String) -> Dictionary:
	return await client.post("/projects/%s/credentials/import" % project_id, {
		"platform": platform,
		"type": type,
		"uuid": uuid
	})


## Get Google OAuth URL for connecting service account
func get_google_auth_url(project_id: String, redirect_uri: String) -> Dictionary:
	var encoded_uri = redirect_uri.uri_encode()
	return await client.fetch("/projects/%s/credentials/android/key/connect?redirectUri=%s" % [project_id, encoded_uri])


## Get a short login link that requires auth
func get_login_link(destination: String) -> Dictionary:
	return await client.post("/me/login-link", {
		"destination": destination,
		"webUrl": client.web_url
	})


## Start the service account setup process for Android
## Returns the initial setup status (status, progress, sub-task flags, etc.)
func start_service_account_setup(project_id: String) -> Dictionary:
	return await client.post("/projects/%s/credentials/android/key/setup/" % project_id, {})


## Get the current service account setup status (for polling)
## Returns {status, progress, errorMessage, hasSignedIn, hasProject, ...}
func get_service_account_setup_status(project_id: String) -> Dictionary:
	return await client.fetch("/projects/%s/credentials/android/key/status/" % project_id)


## Revoke the Google org policy that blocks service account key creation
## DELETE /me/google/policy - returns updated GoogleStatusResponse
func revoke_google_org_policy() -> Dictionary:
	return await client.delete("/me/google/policy")


## Invite the service account to a Google Play developer account
func invite_service_account(project_id: String, developer_id: String) -> Dictionary:
	return await client.post("/projects/%s/credentials/android/key/invite/" % project_id, {
		"developerId": developer_id
	})
